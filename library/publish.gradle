apply plugin: 'maven-publish'

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

def versionName = { ->
    return "2.1.6"
}

def artificatId = { ->
    return "gpuimage"
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId 'io.onelightapps.android'
            artifactId artificatId()
            version versionName()
            artifact("$buildDir/outputs/aar/library-release.aar")
            
            pom.withXml {
                final dependenciesNode = asNode().appendNode('dependencies')
                ext.addDependency = { Dependency dep, String scope ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return
                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', scope)
                    if (!dep.transitive) {
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }
                configurations.api.getDependencies().each { dep -> addDependency(dep, "compile") }
                configurations.implementation.getDependencies().each { dep -> addDependency(dep, "runtime") }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/OnelightApps/android-gpuimage")

            credentials {
                username = localProperties['github.username'] ?: System.getenv("GPR_USER")
                password = localProperties['github.token'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}